# Test that requestmark works
# Well, there is no simple way to test it as it 
# requires TAC.
#
$clockstart:0.1
$include:"testuserinfo.rwl"

database testdefault
  username test_user
  password test_pass
  connect test_cstring || ":dedicated"
  dedicated requestmark
  default;

string(20) ename, dname;
integer empno, deptno;
string mysid;

sql seldept
select deptno, dname,sys_context('USERENV','SID') from dept
order by deptno
/
  array 10;
  define 1 deptno, 2 dname ;
  define 3 mysid;
end;

sql selemp
select * from (
select empno, ename from emp
where deptno = :deptno
order by ename
)
where rownum < 10
/
  array 10;
  define 1 empno, 2 ename;
  bind ":deptno" deptno ;
end;

integer threads sum rowsgotten := 0;

procedure getemps()
  for selemp loop
    $if $DEBUG162 != "" $then printline "emp:", empno, ename, deptno, dname; $endif
    rowsgotten := rowsgotten+1;
  end;
  $if $DEBUG162 != "" $then printline "--end--", dname; $endif
end;

string lastsid := "notasid";

procedure getdepts()
  for seldept loop
    getemps();
  end;
  $if $DEBUG162 != "" $then printline mysid; $endif
  if mysid != lastsid then
    printline "New SID";
  end if;
  lastsid := mysid;
end;

run
  threads 1
    for wait 0.1 count 10 loop getdepts(); end; 
  end;
end;

# printvar all;

printline "rowsgotten:", rowsgotten;
