# sqlreport.rwl
#
# Copyright (c) 2022 Oracle Corporation
# Licensed under the Universal Permissive License v 1.0
# as shown at https://oss.oracle.com/licenses/upl/
#
# History
#
# bengsig  28-apr-2022 - Exit on connect error
# bengsig  13-apr-2022 - Change output's to use hyphen
# bengsig         2018 - Creation
#
#
# usage:
#
# rwloadsim -ul system/{password}@//host/database sqlreport.rwl sqlid
#
# output: sqlid.html

# If we didn't connect, don't do anything
if oraerror is not null and oraerror then
  exit 1;
end if;

$longoption:file-count=1

string(4100) output_file;
$useroption:output-file:"--output-file=filename	: use specified filename in stead of sqlid.html"

string(4100) output_directory := "." ;
$useroption:output-directory:"--output-directory=directory	: put output file in this directory"

$oraerror:stop

string(13) sqlid;

if $# != 1 then
  writeline stderr, "\nusage: rwloadsim [--userhelp] [options] -ul system/{password}@//host/database sqlreport.rwl sqlid";
  exit 1;
end if;

sqlid := $1;

$mute:46

string(10000000) txt;

clob res;

sql rep_sql
  begin
  :1 := dbms_sqltune.report_sql_detail
  ( sql_id => :2
  , type => 'active'
  , report_level => 'ALL');
  end;
  /
  bindout 1 res;
  bind 2 sqlid;
end;

rep_sql;

file out;
execute 

  readlob res, txt;

  if output_file = "" then
    out := output_directory "/" sqlid || ".html";
  else
    out := output_directory "/" output_file;
  end if;

  writeline out, txt;
  out := null;
end;

