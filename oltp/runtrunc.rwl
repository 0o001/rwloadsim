# Copyright (c) 2021 Oracle Corporation
# Licensed under the Universal Permissive License v 1.0
# as shown at https://oss.oracle.com/licenses/upl/

# bengsig  25-oct-2022 - External control
# bengsig         2019 - Creation

# Truncate old partitions in orders and order_items

$include:"parameters.rwl"
$include:"oe_user.rwl"
$if xc_enabled $then
 $include:"results.rwl"
 $include:"xc_implementation.rwl"
$endif


$oraerror:stop

procedure trunctables() nostatistics

  alter table order_items drop partition for (0)
  /

  truncate table orders_dummy
  /

  alter table orders
  exchange partition for (0)
  with table orders_dummy
  including indexes
  /

  alter table orders
  drop partition for (0)
  /

end trunctables;

private integer pcount := 99999;

private sql get_pcount  /* runtrunc sql 1 */
  select count(*) pcount from user_tab_partitions where table_name = 'ORDERS';
  define sql;
end;

run

$if xc_enabled $then
  threads 1 at rwloadsim
    for every xc_refresh stop runperiod loop
      xc_read(0);
      if xc_stopnow=1 or xc_stopnow=2 then break; end if;
    end loop;
  end threads;
$endif

  threads 1 at rwl_oe_pool


    for stop runperiod / 2 - 10 loop
      get_pcount;
      if pcount<=25 $if xc_enabled $then or xc_stopnow=1 or xc_stopnow=2 $endif then
        return;
      end if;
      printline "truncate oldest partitions in orders and order_items", pcount, runseconds;
      fflush stdout;
      trunctables();
    end loop;

  end threads;
end run;

get_pcount at rwl_oe_pool;
printline "No more partition truncate to do", pcount, runseconds;
