# Test break from cqn
$include:"testuserinfo.rwl"
$include:"testresults.rwl"

database p
  username test_user
  password test_pass
  connect test_cstring
  sessionpool 1 .. 2;

database cqn
  username test_user
  password test_pass
  connect test_cstring
  threads dedicated;

integer runbreak := 10;
$useroption:runbreak

integer runperiod := 30;
$useroption:runperiod

$startseconds:1
$randseed:0xdeadbeef

integer a;
double b;

execute at p
  drop table cqn purge;

  create table cqn
  ( a number primary key not null
  , b number
  );

  for a := 1 .. 5 loop
    b := uniform(0.0,100.0);
    insert into cqn values(:a,:b);
  end loop;
  commit;
end;

procedure showcqn()
  for
    select a,b from cqn order by a;
  loop
    printf "%d %10.4f\n", a,b;
  end loop;
  break querynotification;
end;

showcqn() at p;

run

  querynotification at cqn start 0 stop runperiod
    when
      a := 3;
      select b from cqn where a=:a;
      printf "b is %10.4f at start\n", b;
    then
      a := 3;
      select b from cqn where a=:a;
      printf "b is %10.4f at %.1f\n", b, runseconds;
      if runseconds > runbreak then
	printline "now break from callback";
	break querynotification;
      end if;
    end;

  threads 1 at p
    for queue every 2 stop runperiod loop
      a := 3;
      b := uniform(200.0,300.0);
      update cqn set b=:b where a=:a
      /
      commit;
    if runseconds > runbreak then
      printline "now break from update loop";
      break;
    end if;
    end loop;
  end;
end run;
printf "%.1f\n", runseconds;
