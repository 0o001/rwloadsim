# CQN test for mutext NOT EXECUTED BY DEFAULT in test.sh
$include:"testuserinfo.rwl"
$include:"testresults.rwl"

database p
  username test_user
  password test_pass
  connect test_cstring
  sessionpool 1 .. 2;

database cqn
  username test_user
  password test_pass
  connect test_cstring
  threads dedicated;

integer runperiod := 8;
$useroption:runperiod

$startseconds:1
$randseed:0xdeadbeef

integer a;
double b;

execute at p
  drop table cqn purge;

  create table cqn
  ( a number primary key not null
  , b number
  );

  for a := 1 .. 5 loop
    b := uniform(0.0,100.0);
    insert into cqn values(:a,:b);
  end loop;
  commit;
end;

run

  querynotification at cqn start runseconds()+0.5 stop runperiod
    when
      a := 3;
      select b from cqn where a=:a;
      printf "b is %10.4f at start\n", b;
      wait 3;
      printline "cqn registration done";
    then
      a := 3;
      select b from cqn where a=:a;
      wait 0.08;
      printf "b is %10.4f at %.1f\n", b, runseconds();
    end querynotification;

  threads 1 at p
    for start 1 queue every 1 stop runperiod loop
      a := 3;
      b := uniform(200.0,300.0);
      update cqn set b=:b where a=:a
      /
      commit;
      printf "b updated to %10.4f at %.1f\n", b, runseconds();
    end loop;
  end;
end run;

