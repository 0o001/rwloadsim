# test direct sql with dml

$include:"testuserinfo.rwl"
$include:"testpool.rwl"
# test implicit bind with dml
# and array insert
integer ordno, linno, refno;
string(100) pl;
double b,c,e,g;

integer human := 0; $userswitch:human
$if human $then
  $debugon:0x800
  printline "Check the generated trace file for array insert";
$else
  $randseed:0xdeadbeef
$endif

# declare the SQL statement that inserts one order


# declare the SQL statement that inserts one order line
integer max; # declarations can be put where needed
integer threads sum totord, totlin;
integer threads sum totget := 0;

# delcare the procedure that inserts an order with a random
# number of order lines
procedure insorder()
  if human then
    sql execute
      alter session set events '10046 trace name context level 12, forever';
    end;
  end if;
  b := uniform(0, 100); # a random value
  # Note how data types can be mixed in expressions (like in awk)
  # Here have a function call, a fixed string, and a variable
  pl := uniform(0,100) 
  || "___________________________________________________________" 
  || b;

  c := uniform(1.5,3.5); 
  sql execute
    insert into rwl_demo_ord
    ( ordno 
    , b 
    , pl 
    , c )
    values
    ( rwl_demo_ordno.nextval
    , :b
    , :pl
    , :c
    ) returning ordno into :ordno
    /
    # Even with implicit, we still need explict bindout
    bindout : ordno ;
  end;
  printline "order header done";
  totord:=totord + 1; # counter orders created

  # Now create a random number of order lines and insert them
  max := uniform(1,10); # up to 10 order lines
  # Here is a for loop like in PL/SQL
  for linno := 1 .. max loop
    refno := uniform(1,200); # see rwltest.sql for where this comes from
    e := uniform(0,10);
    # erlang2 is erlang distributed with k=2
    pl := erlang2(1) || "abcabcabcabc";
    # array DML is not yet supported, but the same syntax will 
    # be used saving to array behind the scenes
    if human then
      printline "refno=" refno ", e=" e ", pl=", pl;
    end if;
    sql execute
      insert into rwl_demo_lin
      ( ordno 
      , linno
      , e 
      , pl 
      , refno)
      values (:ordno, :linno, :e, :pl, :refno)
      /
      array 5;
    end;

    totlin:=totlin + 1; # counter order lines created
  end; 
  # Once array DML is supported, this is needed to flush it
  # flush inslinsql;
  # The commit will either implicitly flush are warn that a flush
  # is missing
  if human then
    printline "ordno=" ordno;
  end if;
  commit; # commit one order and its lines
end;

insorder() at testpool; 

for 
sql execute
  select linno, refno, pl
  from rwl_demo_lin
  where ordno = :ordno;
end
at testpool loop
  printline linno, refno, pl;
end loop;

